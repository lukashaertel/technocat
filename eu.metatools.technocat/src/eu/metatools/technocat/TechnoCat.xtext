grammar eu.metatools.technocat.TechnoCat hidden(WS, SL_COMMENT)

import "http://www.eclipse.org/emf/2002/Ecore" as ecore
generate technoCat "http://www.metatools.eu/technocat/TechnoCat"

TechnologyCatalog:
	'model' name=TCID modelElements+=Element*;

Element:
	Import | ModelElement;

Import:
	'import' ref=[TechnologyCatalog|TCID];

ModelElement:
	DefinitionElement | InstanceElement;

DefinitionElement:
	ETD | RTD;

InstanceElement:
	ED | RD;

ETD:
	'type' name=ETID
	('from' bases+=[ETD|ETID] (',' bases+=[ETD|ETID])*)?
	('=' value=STRING)?;

RTD:
	'relation' name=RTID
	('from' bases+=[RTD|RTID] (',' bases+=[RTD|RTID])*)?
	('=' value=STRING)? items+=RTDItem*;

RTDItem:
	domain=[ETD|ETID] '->' codomain=[ETD|ETID]
	('=' value=STRING)?;

ED:
	type=[ETD|ETID] ':' items+=EDItem (',' items+=EDItem)*;

EDItem:
	name=EID
	('=' value=STRING)?;

RD:
	left=[EDItem|EID] relation=[RTD|RTID] right=[EDItem|EID]
	('=' value=STRING)?;

	// Techno-Cat identifier 
TCID:
	ID;

	// Relationship type identifier
RTID:
	ID | STRING | RSYM;

	// Entity type identifier
ETID:
	ID | STRING;

	// Entity identifier
EID:
	ID | STRING;

	// List of overloadable relationship symbols
RSYM:
	"=" | "+" | "-" | "*" | "/" | "%" | "++" | "--" |
	"==" | "!=" | ">" | "<" | ">=" | "<=" |
	"!" | "&&" | "||" |
	"~" | "&" | "|" | "^" | "<<" | ">>" |
	"+=" | "-=" | "*=" | "/=" | "%=" |
	"&=" | "|=" | "^=" | "<<=" | ">>=" |
	"?";

terminal ID:
	'^'? ('a'..'z' | 'A'..'Z' | '_') ('a'..'z' | 'A'..'Z' | '_' | '0'..'9')*;

terminal STRING:
	'"' ('\\' ('b' | 't' | 'n' | 'f' | 'r' | 'u' | '"' | "'" | '\\') | !('\\' | '"'))* '"';

terminal SL_COMMENT:
	'--' !('\n' | '\r')* ('\r'? '\n')?;

terminal WS:
	(' ' | '\t' | '\r' | '\n')+;